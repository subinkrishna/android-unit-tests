apply plugin: 'com.android.application'
apply plugin: 'jacoco'

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.subinkrishna.android.unittest"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {

        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

// Jacoco configuration
jacoco {
    toolVersion = "0.7.1.201405082137"
    reportsDir = file("${buildDir}/reports/jacoco")
}

// Jacoco coverage task
task codeCoverageReport (type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"

    reports {
        xml.enabled false
        csv.enabled false
        // Location to find the coverage reports
        html.destination "${buildDir}/reports/jacoco/html"
    }

    classDirectories = fileTree(
            dir: 'build/intermediates/classes/debug',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewInjector*.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*'])

    sourceDirectories = files([
            'src/main/java',
            'build/generated/source/buildConfig/debug',
            'build/generated/source/r/debug'])

    executionData = files('build/jacoco/testDebugUnitTest.exec')
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'

    testCompile 'junit:junit:4.10'
    testCompile 'org.mockito:mockito-all:1.9.5'
}
